description = 'Auto services'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.h2database:h2:1.3.170'
        classpath 'org.flywaydb:flyway-gradle-plugin:3.2.1'
    }
}

apply plugin: 'java'
// groovy is required for Spock
apply plugin: 'groovy'

apply from: 'gradle/code-coverage.gradle'
apply from: 'gradle/code-quality.gradle'
apply from: 'gradle/application.gradle'
apply from: 'gradle/flywaydb.gradle'

sourceCompatibility = targetCompatibility = 1.8

version = '1.0.0'

sourceSets {
    main {
        java {
            srcDir 'src/java'
        }
        resources {
            srcDir 'src/resources'
        }
    }
    test {
        groovy {
            srcDir 'test/groovy'
        }
    }
}

ext {
    groovyMajorVersion = 2.4
    groovyMinorVersion = 5
}

repositories {
    mavenCentral()
}

dependencies {
    compile "com.fasterxml.jackson.core:jackson-annotations:2.5.1"
    // required dropwizard modules
    ['core', 'views', 'assets', 'jdbi'].each { module ->
        compile "io.dropwizard:dropwizard-$module:0.8.4"
    }
    compile 'com.h2database:h2:1.3.170'

    testCompile "org.codehaus.groovy:groovy-all:${groovyMajorVersion}.${groovyMinorVersion}"
    testCompile "org.spockframework:spock-core:1.0-groovy-${groovyMajorVersion}"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}

task checkCoverage(dependsOn: ['test', 'jacocoTestReport']) {
    description 'Runs all tests and JaCoCo coverage'
    doLast {
        def reports = [test, jacocoTestReport].collect({
            def buildPath = buildDir.path
            def buildDirName = buildPath[buildPath.lastIndexOf(File.separator)..-1]
            '.' + buildDirName + it.reports.html.destination.path - buildPath
        })
        println "\n\tReports are avaialable at ${reports[0]} and ${reports[1]}"
    }
}
